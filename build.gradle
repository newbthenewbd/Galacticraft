def mcVersion = "1.10.2"
def versionMajor = 4
def versionMinor = 0
def versionRev = 0

// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceSets.main {
    java {
        srcDirs = ['src/main/java', 'dependencies/main/java']
    }
    resources {
        srcDirs = ['src/main/resources', 'dependencies/main/resources']
    }
}

def versionBuild = 0
if (System.getenv("BUILD_NUMBER") != null)
    versionBuild = System.getenv("BUILD_NUMBER")
	
version = "${mcVersion}-${versionMajor}.${versionMinor}.${versionRev}.${versionBuild}-BETA" // Set the version number
group= "micdoodle8.mods.galacticraft"
archivesBaseName = "GalacticraftCoreAll"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.10.2-12.18.3.2221"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20161111"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

// Keep a list of dependencies files/directories for easy jar packing
ext.dependencies = ['codechicken/**',
					'**/nei_at.cfg',
					'buildcraft/**',
					'cofh/**',
					'ic2/**',
					'mekanism/**']
					
// Keep a list of core files/directories for easy jar packing
ext.apiFiles = ['micdoodle8/mods/galacticraft/api/**']
					
// Keep a list of core files/directories for easy jar packing
ext.coreFiles = ['micdoodle8/mods/galacticraft/core/**',
					'assets/minecraft/**',
					'assets/galacticraftcore/**',
					'**/dependenciesGC.info']
					
// Keep a list of planets addon files/directories for easy jar packing
ext.planetsFiles = ['micdoodle8/mods/galacticraft/planets/**',
					'assets/galacticraftplanets/**']

ext.coreJar = "build/libs/" + archivesBaseName + "-" + version + ".jar"

// Configure the main jar
jar
{
	// Add necessary files, remove unnecessary ones.
	includes.addAll(project.ext.coreFiles)
	includes.addAll(project.ext.apiFiles)
	includes.addAll('mcmod.info')
	includes.addAll('LICENSE.txt')
	includes.addAll('api/player/**')
	includes.addAll(project.ext.dependencies)
	includes.addAll(project.ext.planetsFiles)
	includes.addAll('**/galacticraftcore_at.cfg')
	includes.addAll('**/galacticraftcore_at.deobf')
}

task moveCoreMcmodInfo(type: Copy, dependsOn: "reobfJar") {
	from 'LICENSE.txt'
	from(zipTree(project.ext.coreJar))
	from 'src/main/resources/assets/galacticraftcore/mcmod.info'
	into 'temporary'
}

task movePlanetsMcmodInfo(type: Copy, dependsOn: "reobfJar") {
	from 'src/main/resources/assets/galacticraftplanets/mcmod.info'
	into 'temporary'
}

task rePackAllJarCore(type: Jar, dependsOn: "moveCoreMcmodInfo") {
	from 'temporary'
}

task rePackAllJarPlanets(type: Jar, dependsOn: "movePlanetsMcmodInfo") {
	from 'temporary'
}

task packCoreJar(type: Jar, dependsOn: "rePackAllJarCore") {
	// Jar will always be updated
	outputs.upToDateWhen {false}
	// Set the jar's base name
	baseName = "GalacticraftCore"
	
	from(zipTree(project.ext.coreJar)) {
		// Add necessary files, remove unnecessary ones.
		includes.addAll('**/galacticraftcore_at.cfg')
		includes.addAll('**/galacticraftcore_at.deobf')
		includes.addAll(project.ext.coreFiles)
		includes.addAll(project.ext.apiFiles)
		includes.addAll('mcmod.info')
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.planetsFiles)
		excludes.addAll(project.ext.dependencies)
	}
	
	// Configure manifest attributes
	manifest
	{
		// Add required coremod manifest attributes
		attributes.put("FMLAT", "galacticraftcore_at.cfg")
	}
}

task packPlanetsJar(type: Jar, dependsOn: "rePackAllJarPlanets") { 
	// Jar will always be updated
	outputs.upToDateWhen {false}
	// Set the jar's base name
	baseName = "Galacticraft-Planets"
	
	from(zipTree(project.ext.coreJar)) {
		// Add necessary files, remove unnecessary ones.
		includes.addAll(project.ext.planetsFiles)
		includes.addAll('mcmod.info')
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.coreFiles)
		excludes.addAll(project.ext.apiFiles)
		excludes.addAll(project.ext.dependencies)
		excludes.addAll('**/galacticraftcore_at.cfg')
		excludes.addAll('**/galacticraftcore_at.deobf')
	}
}

task packCoreJarDev(type: Jar, dependsOn: "classes") {
	from (sourceSets.main.output) {
		includes.addAll('**/galacticraftcore_at.cfg')
		includes.addAll('**/galacticraftcore_at.deobf')
		includes.addAll(project.ext.coreFiles)
		includes.addAll(project.ext.apiFiles)
		includes.addAll('mcmod.info')
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.planetsFiles)
		excludes.addAll(project.ext.dependencies)
	}
	
	from (sourceSets.main.allSource) {
		includes.addAll('**/galacticraftcore_at.cfg')
		includes.addAll('**/galacticraftcore_at.deobf')
		includes.addAll(project.ext.coreFiles)
		includes.addAll(project.ext.apiFiles)
		includes.addAll('mcmod.info')
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.planetsFiles)
		excludes.addAll(project.ext.dependencies)
		excludes.addAll('assets')
		into 'src'
	}
	
	// Configure manifest attributes
	manifest
	{
		// Add required coremod manifest attributes
		attributes.put("FMLAT", "galacticraftcore_at.cfg")
	}
	
	// Jar will always be updated
	outputs.upToDateWhen {false}
	// Set the jar's base name
	baseName = "GalacticraftCore-Dev"
}

task packPlanetsJarDev(type: Jar, dependsOn: "classes") {
	// Jar will always be updated
	outputs.upToDateWhen {false}
	// Set the jar's base name
	baseName = "Galacticraft-Planets-Dev"
	
	from (sourceSets.main.output) {
		// Add necessary files, remove unnecessary ones.
		includes.addAll(project.ext.planetsFiles)
		includes.addAll('mcmod.info')
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.coreFiles)
		excludes.addAll(project.ext.apiFiles)
		excludes.addAll(project.ext.dependencies)
		excludes.addAll('**/galacticraftcore_at.cfg')
		excludes.addAll('**/galacticraftcore_at.deobf')
	}
	
	from (sourceSets.main.allSource) {
		// Add necessary files, remove unnecessary ones.
		includes.addAll(project.ext.planetsFiles)
		includes.addAll('mcmod.info')
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.coreFiles)
		excludes.addAll(project.ext.apiFiles)
		excludes.addAll(project.ext.dependencies)
		excludes.addAll('**/galacticraftcore_at.cfg')
		excludes.addAll('**/galacticraftcore_at.deobf')
		excludes.addAll('assets')
		into 'src'
	}
}

task packApi(type: Jar, dependsOn: "classes") {
	// Jar will always be updated
	outputs.upToDateWhen {false}
	// Set the jar's base name
	baseName = "Galacticraft-API"
	
	from (sourceSets.main.allSource) {
		// Add necessary files, remove unnecessary ones.
		includes.addAll(project.ext.apiFiles)
		includes.addAll('LICENSE.txt')
		excludes.addAll(project.ext.planetsFiles)
		excludes.addAll(project.ext.dependencies)
		excludes.addAll('**/*.info')
		excludes.addAll('assets')
	}
}
